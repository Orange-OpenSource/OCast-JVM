apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}
buildscript {
    ext {
        // Kotlin
        kotlin_version = '1.3.31'

        // Support library
        support_lib_version = '28.0.0'

        // Other libs
        okhttp_version = '3.14.0'

        // Architecture components (livedata, viewmodels...)
        arch_components_version = '1.1.1'

        ktlint_version = '0.31.0'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.ocast.sample.mobile"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 2
        versionName "2.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    ktlint "com.github.shyiko:ktlint:$ktlint_version"

    implementation project(':OCast')
    //implementation project(':mediaroute')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support:mediarouter-v7:$support_lib_version"
    implementation "com.android.support:support-v4:$support_lib_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"

    // Adding view model (livedata is embedded)
    implementation "android.arch.lifecycle:extensions:$arch_components_version"
    implementation "android.arch.lifecycle:viewmodel:$arch_components_version"

    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--reporter=plain", "--reporter=plain,output=${buildDir}/ktlint.txt", "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
